# Just configuration file for running commands.
#
# For more information, visit https://just.systems.

set script-interpreter := ["nu"]
set shell := ["nu", "--commands"]
export PATH := if os() == "windows" {
  justfile_directory() / ".vendor/bin;" + env("PATH")
} else {
  justfile_directory() / ".vendor/bin:" + env("PATH")
}

# Build distribution package.
build:
  uv build --out-dir build/dist

# Execute CI workflow commands.
ci: setup lint doc build test

# Build documentation.
[script]
doc:
  mkdir build/site
  cp README.md doc/index.md
  cp CONTRIBUTING.md doc/
  uv run mkdocs build --strict

# Fix code formatting.
format +paths=".":
  {% if cookiecutter.project_prettier -%}
  deno run --allow-all npm:prettier --write {{ "{{" }}paths{{ "}}" }}
  {% endif -%}
  uv run ruff format {{ "{{" }}paths{{ "}}" }}

# Install project programs.
[script]
install: build
  python3 -m pip install (ls build/dist/*.whl | get name | first)

# Run code analyses.
lint +paths=".":
  {% if cookiecutter.project_prettier -%}
  deno run --allow-all npm:prettier --check {{ "{{" }}paths{{ "}}" }}
  {% endif -%}
  uv run ruff format --check {{ "{{" }}paths{{ "}}" }}
  uv run ruff check {{ "{{" }}paths{{ "}}" }}
  uv run ty check {{ "{{" }}paths{{ "}}" }}

# List all commands available in justfile.
[default]
list:
  @just --list

# Wrapper to Nushell.
[no-exit-message]
@nu *args:
  nu {{ "{{" }}args{{ "}}" }}

# Install development dependencies.
[script]
setup: _setup
  {% if cookiecutter.project_prettier -%}
  if (which deno | is-empty) {
    print "Installing Deno."
    http get https://scruffaluff.github.io/picoware/install/deno.nu
    | nu -c $"($in | decode); main --preserve-env --dest .vendor/bin"
  }
  print $"Using (deno -V)."
  {% endif -%}
  if (which uv | is-empty) {
    print "Installing Uv."
    http get https://scruffaluff.github.io/picoware/install/uv.nu
    | nu -c $"($in | decode); main --preserve-env --dest .vendor/bin"
  }
  print $"Using (uv --version)."
  print "Installing Python packages with Uv."
  if ($env.JUST_INIT? | is-empty) {
    uv sync --locked
  } else {
    uv sync
  }

[unix]
_setup:
  #!/usr/bin/env sh
  set -eu
  if [ ! -x "$(command -v nu)" ]; then
    echo 'Installing Nushell'.
    curl --fail --location --show-error \
      https://scruffaluff.github.io/picoware/install/nushell.sh | sh -s -- \
      --preserve-env --dest .vendor/bin
  fi
  echo "Using Nushell $(nu --version)."

[windows]
_setup:
  #!powershell.exe
  $ErrorActionPreference = 'Stop'
  $ProgressPreference = 'SilentlyContinue'
  $PSNativeCommandUseErrorActionPreference = $True
  if (-not (Get-Command -ErrorAction SilentlyContinue nu)) {
    Write-Output 'Installing Nushell.'
    $NushellScript = Invoke-WebRequest -UseBasicParsing -Uri `
      https://scruffaluff.github.io/picoware/install/nushell.ps1
    Invoke-Expression "& { $NushellScript } --preserve-env --dest .vendor/bin"
  }
  Write-Output "Using Nushell $(nu --version)."

# Run test suites.
test *args:
  uv run pytest --cov {{ "{{" }}args{{ "}}" }}

# Wrapper to Uv.
[no-exit-message]
@uv *args:
  uv {{ "{{" }}args{{ "}}" }}
