# Just configuration file for running commands.
#
# For more information, visit https://just.systems.

set script-interpreter := ["nu"]
set shell := ["nu", "--commands"]
set unstable := true
set windows-shell := ["nu", "--commands"]
export PATH := if os() == "windows" {
  justfile_dir() / ".vendor/bin;" + env("Path")
} else {
  justfile_dir() / ".vendor/bin:" + env("PATH")
}

# List all commands available in justfile.
list:
  just --list

# Execute CI workflow commands.
ci: setup lint build test

# Build application.
[script]
build:
  deno run --allow-all npm:vite build

# Launch application in developer mode.
dev host='127.0.0.1':
  deno run --allow-all npm:vite dev --host {{ "{{" }}host{{ "}}" }}

# Fix code formatting.
format:
  deno run --allow-all npm:prettier --write .

# Run code analyses.
lint:
  deno run --allow-all npm:prettier --check .
  deno run --allow-all npm:eslint .
  deno run --allow-all npm:vue-tsc --build

# Install development dependencies.
[script]
setup: _setup
  if (which deno | is-empty) {
    http get https://scruffaluff.github.io/picoware/install/deno.nu
    | nu -c $"($in | decode); main --preserve-env --dest .vendor/bin"
  }
  deno --version
  if ($env.JUST_INIT? | is-empty) {
    deno install --frozen
  } else {
    deno install
  }

[unix]
_setup:
  #!/usr/bin/env sh
  set -eu
  if [ ! -x "$(command -v nu)" ]; then
    curl --fail --location --show-error \
      https://scruffaluff.github.io/picoware/install/nushell.sh | sh -s -- \
      --preserve-env --dest .vendor/bin
  fi
  echo "Nushell $(nu --version)"

[windows]
_setup:
  #!powershell.exe
  $ErrorActionPreference = 'Stop'
  $ProgressPreference = 'SilentlyContinue'
  $PSNativeCommandUseErrorActionPreference = $True
  if (-not (Get-Command -ErrorAction SilentlyContinue nu)) {
    $NushellScript = Invoke-WebRequest -UseBasicParsing -Uri `
      https://scruffaluff.github.io/picoware/install/nushell.ps1
    Invoke-Expression "& { $NushellScript } --preserve-env --dest .vendor/bin"
  }
  Write-Output "Nushell $(nu --version)"

# Run test suites.
test *args:
  deno run allow-all npm:vitest {{ "{{" }}args{{ "}}" }}

